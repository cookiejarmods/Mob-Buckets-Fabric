plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.mod_id
group = project.maven_group
version = System.env.GITHUB_ACTIONS == "true"
		? "${project.mod_version}+build.${System.env.GITHUB_RUN_NUMBER}"
		: project.mod_version

repositories {
	maven {
		name = "Jitpack"
		url  = "https://jitpack.io/"
	}

	maven {
		name = "Shadew's Maven"
		url  = "https://maven.shadew.net/"
	}

	maven {
		name = "Gegy's Maven"
		url  = "https://maven.gegy.dev/"
	}

	maven {
		name = "Terraformers Maven"
		url  = "https://maven.terraformersmc.com/releases/"
	}

	maven {
		name = "Quilt Maven"
		url  = "https://maven.quiltmc.org/repository/release/"
	}

	maven {
		name = "CottonMC"
		url = "https://server.bbkr.space/artifactory/libs-release"
	}

	maven {
		name = "Shedaniel Maven"
		url = "https://maven.shedaniel.me/"
	}
}

dependencies {
	// Required
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Libraries
	modImplementation ("com.terraformersmc:modmenu:${project.mod_menu_version}") { exclude group: "net.fabricmc.fabric-api" }
	modImplementation (include("io.github.cottonmc:LibCD:${project.libcd_version}"))

	//modImplementation("net.shadew:jedt:${project.jedt_version}") { exclude group: "net.fabricmc.fabric-api" }

	// Compatibility

	// Utilities
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:7.0.343"
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

loom {
	accessWidenerPath = file("src/main/resources/mobbuckets.accesswidener")

	runs {
		datagen {
			server()

			name "Minecraft Data"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"

			runDir "build/datagen"
		}
	}
}
assemble.dependsOn runDatagen

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
	from("LICENSE_ASSETS") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {}
}